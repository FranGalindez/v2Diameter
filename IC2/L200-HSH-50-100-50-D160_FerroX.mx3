//----------------------------------------------------------
//Important variables
//----------------------------------------------------------
alpha = 0.5
d := 160 // cylinder of diameter
diameter := 160e-9
l := 200 // cylinder height
ACo := 31e-12
MCo := 1440e3
K1Co := 410e3
ANi := 8e-12
MNi := 490e3
K1Ni := -5e3
cl := 2.5

second_long := 100e-9
length := 200e-9

//----------------------------------------------------------
//Universe and cells
//----------------------------------------------------------

SetGridSize(d/cl,d/cl,l/cl)
SetCellSize(2.5e-9, 2.5e-9, 2.5e-9)

//----------------------------------------------------------
//Geometry
//----------------------------------------------------------

a := cylinder(diameter, length)
setgeom( a )
saveas(m, "geom")

//----------------------------------------------------------
//Regions
//----------------------------------------------------------

defregion(1, zrange(-(length/2), -(second_long/2)))  // first cylinder 
defregion(2, zrange(-(second_long/2),second_long/2))   // second cylinder  
defregion(3, zrange(second_long/2, length/2))   // third cylinder   
save(regions)

//----------------------------------------------------------
//Parameters region 1
//----------------------------------------------------------
  
Msat.setregion(1, MCo) //material parameters of Co (Hard)
Aex.setregion(1, ACo)
Ku1.setregion(1, K1Co)
anisU.setregion(1, vector(0,0,1))

     
//----------------------------------------------------------
//Parameters region 2
//----------------------------------------------------------

Msat.setregion(2, MNi) //material parameters of Py (Soft)
Aex.setregion(2, ANi)
Ku1.setregion(2, K1Ni)
anisU.setregion(2, vector(0,0,1))

//----------------------------------------------------------
//Parameters region 3
//----------------------------------------------------------

Msat.setregion(3, MCo)    //material parameters of Co (Hard)
Aex.setregion(3, ACo)
Ku1.setregion(3, K1Co)
anisU.setregion(3, vector(0,0,1))

//----------------------------------------------------------
//initial magnetization states
//----------------------------------------------------------

m = Uniform(1,0.1,0)
saveas(m, "Ferro_X")

TableAdd(m_full)
TableAdd(B_ext)
TableAdd(E_exch)
TableAdd(E_demag)
TableAdd(E_zeeman)
TableAdd(E_total)

runwhile(maxtorque > 1e-4) 
Tablesave()
Save(m)

